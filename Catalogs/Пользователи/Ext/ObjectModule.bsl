

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	ВыполняемоеДействие = Неопределено;
		
	//Новый элемент создаем на сервисе, измененный - обновляем, 
	//помеченный на удаление - удаляем, при снятии пометки удаления - создаем вновь
	
	Если ЭтоНовый() Тогда

		ВыполняемоеДействие 		= Перечисления.ВыполняемыеДействияRestApi.Создание;
		
	Иначе

		ПометкаУдаленияТекущая 		= ПолучитьУстановленноеЗначениеПометкиНаУдаление();		
		ПометкаНаУдалениеИзменена 	= ?(ПометкаУдаления = ПометкаУдаленияТекущая, Ложь, Истина);
		
		Если ПометкаНаУдалениеИзменена Тогда
			
			Если ПометкаУдаления Тогда			
				ВыполняемоеДействие = Перечисления.ВыполняемыеДействияRestApi.Удаление;				
			Иначе				
				ВыполняемоеДействие = Перечисления.ВыполняемыеДействияRestApi.Создание;				
			КонецЕсли;

		ИначеЕсли НЕ ПометкаУдаления Тогда
			
			ВыполняемоеДействие = Перечисления.ВыполняемыеДействияRestApi.Обновление;
			
	    КонецЕсли;
		
	КонецЕсли;
	
	ТекстОшибки = "";
		
	Если ВыполняемоеДействие = Перечисления.ВыполняемыеДействияRestApi.Создание Тогда

		СтруктураПользователя 	= ПолучитьСтруктуруПользователяДляСервиса();		
		Id_ВСтороннемСервисе 	= ВыполнениеЗапросовAPI.СоздатьПользователяНаСервисе(СтруктураПользователя, Отказ, ТекстОшибки);
		
	ИначеЕсли ВыполняемоеДействие = Перечисления.ВыполняемыеДействияRestApi.Обновление Тогда

		СтруктураПользователя 	= ПолучитьСтруктуруПользователяДляСервиса();		
		ВыполнениеЗапросовAPI.ОбновитьПользователяНаСервисе(СтруктураПользователя, Id_ВСтороннемСервисе, Отказ, ТекстОшибки);
		
	ИначеЕсли ВыполняемоеДействие = Перечисления.ВыполняемыеДействияRestApi.Удаление Тогда

		СтруктураПользователя 	= ПолучитьСтруктуруПользователяДляСервиса();		
		ВыполнениеЗапросовAPI.УдалитьПользователяНаСервисе(Id_ВСтороннемСервисе, Отказ, ТекстОшибки);		
		
	КонецЕсли;

	
	Если Отказ Тогда

		Сообщение 		= новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось выполнить действие """ + ВыполняемоеДействие + """ на сервисе Rest-api. " + Символы.ПС + ТекстОшибки;
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруПользователяДляСервиса()
	
	СтруктураПользователя = Новый Структура;
	
	СтруктураПользователя.Вставить("name" , Наименование);
	СтруктураПользователя.Вставить("job"  , Работа);
	
	Возврат СтруктураПользователя;

КонецФункции

Функция ПолучитьУстановленноеЗначениеПометкиНаУдаление()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ПометкаНаУдалениеПоСсылке =  ВыборкаДетальныеЗаписи.ПометкаУдаления;
	Иначе
		ПометкаНаУдалениеПоСсылке = Неопределено;
	КонецЕсли;
	
	Возврат ПометкаНаУдалениеПоСсылке;

КонецФункции

#КонецОбласти



